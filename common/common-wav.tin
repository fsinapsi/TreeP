;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defun wav-open (path) net wav-open)
(defnet wav-open (path @info)
        (deflocal f)

        (set f (fopenro path))
        (<> f undef)
        (alt    (wav-open-wav f @info)
                (wav-open-sox f @info)
                (seq    (clr @info)
                        (close f) )))

(defnet wav-open-wav (f @info)
        (deflocal ch frequency samplesize bps samples offset datasize)

        (fsetpos 0 f)
        (= (freadstr f 4) "RIFF")
        (skip (freaduint-le f 32))
        (= (freadstr f 8) "WAVEfmt ")
        (set offset (freaduint-le f 32))
        (>= offset 16)
        (skip (freaduint-le f 16))
        (set ch (freaduint-le f 16))
        (set frequency (freaduint-le f 32))
        (skip (freaduint-le f 32))
        (set samplesize (freaduint-le f 16))
        (set bps (freaduint-le f 16))
        (fsetpos (+ offset 20) f)
        (while (<> (freadstr f 4) "data") do
                (set offset (freaduint-le f 32))
                (fsetpos (+ (fpos f) offset) f) )
        (set datasize (freaduint-le f 32))
        (set offset (fpos f))
        (set samples (div (min datasize (- (length f) offset)) samplesize))
        (> samples 0)
        (set @info (assoc))
        (set <@info "codec"> "WAV")
        (set <@info "type"> "read")
        (set <@info "fp"> f)
        (set <@info "channels"> ch)
        (set <@info "frequency"> frequency)
        (set <@info "samplesize"> samplesize)
        (set <@info "bps"> bps)
        (set <@info "samples"> samples)
        (set <@info "offset"> offset)
        (set <@info "datasize"> datasize) )

(defnet wav-open-sox (f @info)
        (deflocal ch frequency samplesize bps samples offset comment-size datasize)

        (fsetpos 0 f)
        (= (freadstr f 4) ".SoX")
        (set offset (freaduint-le f 32))
        (skip (freaduint-le f 64))
        (set frequency (floor (freadfloat-le f 64)))
        (set ch (freaduint-le f 32))
        (set comment-size (freaduint-le f 32))
        (<= comment-size (- 0xFFFFFFFF (+ 4 8 8 4 4) 4))
        (> frequency 0)
        (< frequency (maxint))
        (> ch 0)
        (< ch (maxint))
        (set samplesize (* 4 ch))
        (set bps 32)
        (inc offset 4)
        (set samples (div (- (length f) offset) samplesize))
        (> samples 0)
        (set datasize (* samples samplesize))
        (set @info (assoc))
        (set <@info "codec"> "SOX")
        (set <@info "type"> "read")
        (set <@info "fp"> f)
        (set <@info "channels"> ch)
        (set <@info "frequency"> frequency)
        (set <@info "samplesize"> samplesize)
        (set <@info "bps"> bps)
        (set <@info "samples"> samples)
        (set <@info "offset"> offset)
        (set <@info "datasize"> datasize) )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defun wav-create (path ref-info) net wav-create)
(defnet wav-create (path ref-info @info)
        (deflocal f ch frequency samplesize bps)

        (set f (fcreate path))
        (<> f undef)
        (set ch (wav-channels ref-info))
        (set frequency (wav-frequency ref-info))
        (set samplesize (wav-samplesize ref-info))
        (set bps (wav-bps ref-info))
        (if (= ch undef)
        then    (set ch 2) )
        (if (= frequency undef)
        then    (set frequency 44100) )
        (if (= samplesize undef)
        then    (set samplesize 4) )
        (if (= bps undef)
        then    (set bps 16) )
        (fprint f "RIFF")
        (fwriteuint-le f 32 0)
        (fprint f "WAVEfmt ")
        (fwriteuint-le f 32 16)
        (fwriteuint-le f 16 1)
        (fwriteuint-le f 16 ch)
        (fwriteuint-le f 32 frequency)
        (fwriteuint-le f 32 (* frequency samplesize))
        (fwriteuint-le f 16 samplesize)
        (fwriteuint-le f 16 bps)
        (fprint f "data")
        (fwriteuint-le f 32 0)
        (set @info (assoc))
        (set <@info "codec"> "WAV")
        (set <@info "type"> "write")
        (set <@info "fp"> f)
        (set <@info "channels"> ch)
        (set <@info "frequency"> frequency)
        (set <@info "samplesize"> samplesize)
        (set <@info "bps"> bps)
        (set <@info "samples"> 0)
        (set <@info "offset"> 44)
        (set <@info "datasize"> 0) )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defnet wav-close (info)
        (deflocal f l)

        (assocp info)
        (set f (wav-fp info))
        (if (= (wav-type info) "write")
        then    (set l (length f))
                (fsetpos 4 f)
                (fwriteuint-le f 32 (- l 8))
                (fsetpos 40 f)
                (fwriteuint-le f 32 (- l 44)) )
        (close f) )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defun wav-open-memory (raw) net wav-open-memory)
(defnet wav-open-memory (raw @info)
        (rawp raw)
        (alt    (wav-open-memory-wav raw @info)
                (wav-open-memory-sox raw @info) ))

(defun wav-open-memory-wav (raw) net wav-open-memory-wav)
(defnet wav-open-memory-wav (raw @info)
        (deflocal ch frequency samplesize bps samples offset datasize)

        (= (raw-readstr raw 0 4) "RIFF")
        (= (raw-readstr raw 8 8) "WAVEfmt ")
        (set offset (raw-readuint-le raw 16 32))
        (>= offset 16)
        (set ch (raw-readuint-le raw 22 16))
        (set frequency (raw-readuint-le raw 24 32))
        (set samplesize (raw-readuint-le raw 32 16))
        (set bps (raw-readuint-le raw 34 16))
        (inc offset 20)
        (while (<> (raw-readstr raw offset 4) "data") do
                (inc offset 4)
                (inc offset (raw-readuint-le raw offset 32) 4) )
        (inc offset 4)
        (set datasize (raw-readuint-le raw offset 32))
        (inc offset 4)
        (set samples (div (min datasize (- (length raw) offset)) samplesize))
        (> samples 0)
        (set @info (assoc))
        (set <@info "codec"> "WAV")
        (set <@info "type"> "read")
        ;(set <@info "fp"> raw)
        (set <@info "channels"> ch)
        (set <@info "frequency"> frequency)
        (set <@info "samplesize"> samplesize)
        (set <@info "bps"> bps)
        (set <@info "samples"> samples)
        (set <@info "offset"> offset)
        (set <@info "datasize"> datasize) )

(defun wav-open-memory-sox (raw) net wav-open-memory-sox)
(defnet wav-open-memory-sox (raw @info)
        (deflocal ch frequency samplesize bps samples offset comment-size datasize)

        (= (raw-readstr raw 0 4) ".SoX")
        (set offset (raw-readuint-le raw 4 32))
        (set frequency (floor (raw-readfloat-le raw 16 64)))
        (set ch (raw-readuint-le raw 24 32))
        (set comment-size (raw-readuint-le raw 28 32))
        (<= comment-size (- 0xFFFFFFFF (+ 4 8 8 4 4) 4))
        (> frequency 0)
        (< frequency (maxint))
        (> ch 0)
        (< ch (maxint))
        (set samplesize (* 4 ch))
        (set bps 32)
        (inc offset 4)
        (set samples (div (- (length raw) offset) samplesize))
        (> samples 0)
        (set datasize (* samples samplesize))
        (set @info (assoc))
        (set <@info "codec"> "SOX")
        (set <@info "type"> "read")
        ;(set <@info "fp"> raw)
        (set <@info "channels"> ch)
        (set <@info "frequency"> frequency)
        (set <@info "samplesize"> samplesize)
        (set <@info "bps"> bps)
        (set <@info "samples"> samples)
        (set <@info "offset"> offset)
        (set <@info "datasize"> datasize) )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defun wav-codec (info)
        <info "codec"> )

(defun wav-type (info)
        <info "type"> )

(defun wav-fp (info)
        <info "fp"> )

(defun wav-channels (info)
        <info "channels"> )

(defun wav-frequency (info)
        <info "frequency"> )

(defun wav-samplesize (info)
        <info "samplesize"> )

(defun wav-bps (info)
        <info "bps"> )

(defun wav-samples (info)
        <info "samples"> )

(defun wav-offset (info)
        <info "offset"> )

(defun wav-datasize (info)
        <info "datasize"> )

(defun wav-duration (info)
        (/ (wav-samples info) (wav-frequency info)) )

(defun wav-sample-val (info sample channel) net wav-sample-val)
(defnet wav-sample-val (info sample channel @val)
        (deflocal samplesize channels samplesize2 f)

        (set samplesize (wav-samplesize info))
        (integerp samplesize)
        (set channels (wav-channels info))
        (set samplesize2 (/ samplesize channels))
        (integerp samplesize2)
        (< sample (wav-samples info))
        (< channel channels)
        (set f (wav-fp info))
        (fsetpos (+ (wav-offset info) (* samplesize sample) (* samplesize2 channel)) f)
        (set @val (freadsint-le f (* samplesize2 8))) )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; converte un timestamp nel numero di campione piÃ¹ vicino

(defun wav-timestamp->sample (info ts) net wav-timestamp->sample)
(defnet wav-timestamp->sample (info ts @sample)
        (deflocal frequency s t)

        (set frequency (wav-frequency info))
        (integerp frequency)
        (if (stringp ts)
        then    (set s ts)
                (set ts 0)
                (opt*   (search-and-cut ":" s t)
                        (set ts (* 60 (+ ts (str->num t)))) )
                (inc ts (str->num s)) )
        (rationalp ts)
        (set @sample (rint (* ts frequency)))
        (>= @sample 0) )

; converte un numero di campione nel timestamp

(defun wav-sample->timestamp (info sample) net wav-sample->timestamp)
(defnet wav-sample->timestamp (info sample @ts)
        (deflocal frequency)

        (set frequency (wav-frequency info))
        (integerp frequency)
        (set @ts (/ sample frequency))
        (>= @ts 0) )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; appende campioni muti

(defnet wav-write-silence (info samples)
        (deflocal fo samplesize rem buf i)

        (= (wav-type info) "write")
        (set fo (wav-fp info))
        (set samplesize (wav-samplesize info))
        (set rem (* samplesize samples))
        (set buf (raw 65536))
        (raw-set buf (int->char 0))
        (while (> rem 0) do
                (set i (min rem 65536))
                (= i (raw-write buf fo i))
                (dec rem i) )
        (close buf)
        (assoc-inc info "samples" samples)
        (assoc-inc info "datasize" (* samples samplesize)) )

; appende un segmento

(defnet wav-write-copy (info src-info beg samples)
        (deflocal fi fo samplesize buf)

        (= (wav-type info) "write")
        (set fi (wav-fp src-info))
        (<> fi undef)
        (set fo (wav-fp info))
        (set samplesize (wav-samplesize src-info))
        (= samplesize (wav-samplesize info))
        (<= beg (wav-samples src-info))
        (set samples (min (- (wav-samples src-info) beg) samples))
        (fsetpos (+ (wav-offset src-info) (* samplesize beg)) fi)
        (set buf (raw 65536))
        (filecopy-basic fi fo (* samplesize samples) buf)
        (close buf)
        (assoc-inc info "samples" samples)
        (assoc-inc info "datasize" (* samples samplesize)) )

; silenzia un segmento in posizione arbitraria

(defnet wav-mute-segment (info beg samples)
        (deflocal fo samplesize rem buf i)

        (= (wav-type info) "write")
        (set fo (wav-fp info))
        (<= beg (wav-samples info))
        (set samples (min (- (wav-samples info) beg) samples))
        (set samplesize (wav-samplesize info))
        (fsetpos (+ (wav-offset info) (* samplesize beg)) fo)
        (set rem (* samplesize samples))
        (set buf (raw 65536))
        (raw-set buf (int->char 0))
        (while (> rem 0) do
                (set i (min rem 65536))
                (= i (raw-write buf fo i))
                (dec rem i) )
        (close buf)
        (fsetpos (length fo) fo) )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; stampa alcune info

(defnet wav-info (info)
        (deflocal duration extrabytes)

        (set duration (wav-duration info))
        (set extrabytes (- (* (wav-samplesize info) (wav-samples info)) (wav-datasize info)))
        (print "canali: " (wav-channels info) nl
               "freq. campionamento: " (format-num (wav-frequency info)) " Hz" nl
               "dimensione campioni: " (wav-samplesize info) " bytes" nl
               "bits per sample: " (wav-bps info) " bit" nl
               "campioni: " (format-num (wav-samples info)) nl
               "offset: " (wav-offset info) nl
               "durata: " (approx6 duration) " s (" (date-s2hhmmss duration) ")" nl
               (if (> extrabytes 0)
                        (+ "note: contiene extra bytes in fondo (" (format-num extrabytes) ")" nl)
                        "" )
               (if (< extrabytes 0)
                        (+ "note: mancano bytes (" (format-num -extrabytes) ")" nl)
                        "" )))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; crea una copia esatta

(defnet wav-copy (path1 path2)
        (deflocal info1 info2)

        (set info1 (wav-open path1))
        (<> info1 undef)
        (set info2 (wav-create path2 info1))
        (<> info2 undef)
        (wav-write-copy info2 info1 0 (wav-samples info1))
        (wav-close info2)
        (wav-close info1) )

; crea una copia muta

(defnet wav-copy-mute (path1 path2)
        (deflocal info1 info2)

        (set info1 (wav-open path1))
        (<> info1 undef)
        (set info2 (wav-create path2 info1))
        (<> info2 undef)
        (wav-write-silence info2 (wav-samples info1))
        (wav-close info2)
        (wav-close info1) )

; crea una copia preceduta da silenzio (ritardo)

(defnet wav-copy-delay (path1 path2 ts)
        (deflocal info1 info2 samples)

        (set info1 (wav-open path1))
        (<> info1 undef)
        (set samples (wav-timestamp->sample info1 ts))
        (integerp samples)
        (> samples 0)
        (set info2 (wav-create path2 info1))
        (<> info2 undef)
        (wav-write-silence info2 samples)
        (wav-write-copy info2 info1 0 (wav-samples info1))
        (wav-close info2)
        (wav-close info1) )

; crea una copia rimuovendo segmento iniziale (anticipo)

(defnet wav-copy-antedate (path1 path2 ts)
        (deflocal info1 info2 samples)

        (set info1 (wav-open path1))
        (<> info1 undef)
        (set samples (wav-timestamp->sample info1 ts))
        (integerp samples)
        (> samples 0)
        (set info2 (wav-create path2 info1))
        (<> info2 undef)
        (wav-write-copy info2 info1 samples (- (wav-samples info1) samples))
        (wav-close info2)
        (wav-close info1) )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defnet wav-play (raw)
        (wav-play-low raw 1 false) )

(defnet wav-play-low (raw vol verbose)
        (deflocal info duration samplesize frequency rem len buf s pos f ts0 ts)

        (set info (if (rawp raw) (wav-open-memory raw) (wav-open raw)))
        (<> info undef)
        (set duration (date-s2hhmmss (wav-duration info)))
        (set samplesize (wav-samplesize info))
        (set frequency (wav-frequency info))
        (set rem (* samplesize (wav-samples info)))
        (if (= rem 0)
        then    (wav-close info)
                (fail) )
        (set len (* samplesize frequency))
        (set buf (raw len))
        (set s (sdl-open-audio-stream (wav-channels info) frequency (wav-bps info)))
        (if (= s undef)
        then    (close buf)
                (wav-close info)
                (fail) )
        (sdl-set-audio-stream-gain s vol)
        (set len (min len rem))
        (set pos (wav-offset info))
        (if (rawp raw)
        then    (raw-copy-from-raw buf raw 0 pos len)
        else    (set f (wav-fp info))
                (fsetpos pos f)
                (set len (min len rem))
                (if (<> (raw-read buf f len) len)
                then    (close buf s)
                        (wav-close info)
                        (fail) ))
        (set ts0 (sdl-get-ticks-ns))
        (set ts (- ts0 1000000000))
        (if verbose
        then    (wav-info info)
                (print "tipo riproduzione: " (if (rawp raw) "memoria" "file") nl
                       "volume: " (approx3 vol) nl ))
        (sdl-resume-audio-stream-device s)
        (opt*   (sdl-put-audio-stream-data s buf len)
                (dec rem len)
                (inc pos len)
                (inc ts (* 1000000000 (/ len samplesize frequency)))
                (if verbose
                then    (print ' ' (date-s2hhmmss (/ (- (sdl-get-ticks-ns) ts0) 1000000000)) '/' duration '\r')
                        (fflush (stdout)) )
                (if (= rem 0)
                then    ; prima di uscire attendiamo ancora 10 ms
                        (inc ts 1000000000 10000000)
                        (sdl-delay-ns (rint (- ts (sdl-get-ticks-ns))))
                        (fail) )
                (set len (min len rem))
                (if (rawp raw)
                then    (raw-copy-from-raw buf raw 0 pos len)
                else    (= (raw-read buf f len) len) )
                (sdl-delay-ns (rint (- ts (sdl-get-ticks-ns)))) )
        (if verbose
        then    (print nl)
                (fflush (stdout)) )
        (close buf s)
        (wav-close info) )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;                                                                      ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

